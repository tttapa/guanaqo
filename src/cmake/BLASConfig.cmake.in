@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

if (@GUANAQO_WITH_MKL@)
    if (NOT TARGET MKL::MKL)
        set(guanaqoBLAS_NOT_FOUND_MESSAGE "MKL not found. Please use find_package(MKL) with the appropriate options before find_package(guanaqo).")
        set(guanaqoBLAS_FOUND False)
        return()
    endif()
elseif(@GUANAQO_WITH_OPENBLAS@)
    find_dependency(OpenBLAS)
else()
    find_dependency(BLAS)
    find_dependency(LAPACK)
endif()

include("${CMAKE_CURRENT_LIST_DIR}/guanaqoBLASTargets.cmake")

# The following features may have been enabled in the $<BUILD_INTERFACE>, but
# disabled in the $<INSTALL_INTERFACE>. If they are supported on the user's
# system, we do enable them again.
if (@GUANAQO_WITH_CXX_23@ AND "cxx_std_23" IN_LIST CMAKE_CXX_COMPILE_FEATURES)
    target_compile_features(guanaqo::blas INTERFACE cxx_std_23)
endif()

check_required_components(guanaqoBLAS)
