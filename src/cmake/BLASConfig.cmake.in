@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

if (@GUANAQO_WITH_MKL@)
    if (NOT DEFINED MKL_THREADING)
        set(MKL_THREADING "@MKL_THREADING@")
    endif()
    if (NOT DEFINED MKL_INTERFACE)
        set(MKL_INTERFACE "@MKL_INTERFACE@")
    endif()
    if (NOT DEFINED MKL_LINK)
        set(MKL_LINK "@MKL_LINK@")
    endif()
    find_dependency(MKL)
elseif(@GUANAQO_WITH_OPENBLAS@)
    find_dependency(OpenBLAS)
else()
    find_dependency(BLAS)
    find_dependency(LAPACK)
endif()

include("${CMAKE_CURRENT_LIST_DIR}/guanaqoBLASTargets.cmake")

# The following features may have been enabled in the $<BUILD_INTERFACE>, but
# disabled in the $<INSTALL_INTERFACE>. If they are supported on the user's
# system, we do enable them again.
if (@GUANAQO_WITH_CXX_23@ AND "cxx_std_23" IN_LIST CMAKE_CXX_COMPILE_FEATURES)
    target_compile_features(guanaqo::blas INTERFACE cxx_std_23)
endif()

check_required_components(guanaqoBLAS)
