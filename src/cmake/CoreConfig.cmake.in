@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

if (@GUANAQO_WITH_ITT@)
    find_dependency(ittapi)
    if (TARGET ittapi::ittapi AND NOT TARGET ittapi::ittnotify)
        add_library(ittapi::ittnotify ALIAS ittapi::ittapi)
    endif()
endif()

include("${CMAKE_CURRENT_LIST_DIR}/guanaqoCoreTargets.cmake")

# The following features may have been enabled in the $<BUILD_INTERFACE>, but
# disabled in the $<INSTALL_INTERFACE>. If they are supported on the user's
# system, we do enable them again.
if (@GUANAQO_WITH_CXX_23@ AND "cxx_std_23" IN_LIST CMAKE_CXX_COMPILE_FEATURES)
    target_compile_features(guanaqo::guanaqo INTERFACE cxx_std_23)
    target_compile_features(guanaqo::linalg INTERFACE cxx_std_23)
endif()

check_required_components(guanaqoCore)
